plugins {
    id 'java'
    id 'kr.entree.spigradle' version '2.2.4'
    id "com.github.johnrengelman.shadow" version "5.1.0"
}

group = 'fr.minemobs'
version = '1.2'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

String missingPermissionMessage = '§8[§4Minemobs Utils§8] §4You do not have the permission!'

repositories {
    mavenCentral()
    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
    maven {
        url = "https://minevolt.net/repo/"
    }
    maven {url = "https://repo.inventivetalent.org/content/groups/public/"}
    maven {
        allowInsecureProtocol = true
        name = "Minemobs Snapshot"
        url = "http://minemobs.galaxyfight.fr:8081/snapshots/"
    }
}

dependencies {
    compileOnly spigot('1.16.5')
    compileOnly 'org.jetbrains:annotations:21.0.1'
    compileOnly('net.skinsrestorer:skinsrestorer:14.1.1') {
        transitive(false)
    }
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.7.0"
    compileOnly 'org.bukkit:craftbukkit:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT'
    implementation 'org.reflections:reflections:0.9.12'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation group: 'de.tr7zw', name: 'item-nbt-api', version: '2.8.0'
}

shadowJar {
    archiveClassifier.set('')
    relocate 'de.tr7zw.changeme', 'fr.minemobs.relocate'
    relocate 'org.reflections', 'fr.minemobs.relocate.reflections'
    relocate 'javassist', 'fr.minemobs.relocate.javassist'
    relocate 'org.bstats', 'fr.minemobs.relocate.bstats'
}

spigot {
    authors 'Minemobs'
    apiVersion '1.16'
    website = "https://github.com/Minemobs/MinemobsUtils"
    softDepends("SkinsRestorer")
    depends("ProtocolLib")
    commands {
        cc {
            aliases 'colorcode', 'chatcolor'
            usage '/<command>'
        }
        craft {
            aliases 'crafting'
            permission 'minemobsutils.craftcommand'
            permissionMessage missingPermissionMessage
            usage '/<command>'
        }
        ec {
            aliases 'enderchest'
            permission 'minemobsutils.enderchest'
            permissionMessage missingPermissionMessage
            usage '/<command> [target] [source]'
        }
        ci {
            aliases 'customitems'
            permission 'minemobsutils.customitems.give'
            permissionMessage missingPermissionMessage
            usage '/<command>'
        }
        ping {
            usage '/<command> [player]'
        }
        customenchant {
            aliases 'ce'
            permission 'minemobsutils.customenchant'
            permissionMessage missingPermissionMessage
            usage '/<command>'
        }
        heal {
            usage '/<command> [player]'
        }
        feed {
            usage '/<command> [player]'
        }
        fly {
            permission 'minemobsutils.fly'
            permissionMessage missingPermissionMessage
            usage '/<command>'
        }
        staffchat {
            aliases 'sc'
            permission 'minemobsutils.mod.chat'
            permissionMessage missingPermissionMessage
            usage '/<command> <message>'
        }
        nick {
            permission 'minemobsutils.nick'
            permissionMessage missingPermissionMessage
            usage '/<command> [player] <nick>'
        }
        broadcast {
            aliases 'bc'
            permission 'minemobsutils.broadcast'
            permissionMessage missingPermissionMessage
            usage '/<command> <message>'
        }
        setcustomblock {
            aliases 'scb'
            permission 'minemobsutils.setcustomblock'
            permissionMessage missingPermissionMessage
            usage '/<command> <block>'
        }
    }

    debug {
        args '--nojline', '--max-players', '100'

        jvmArgs '-Xmx2G'
        serverPort 25565
    }
}

task copyFile(type: Copy) {
    /**
     * Check if it's minemobs's computer
     */
    if(InetAddress.localHost.hostName != "DESKTOP-USNU2V8") return
    from file("$buildDir/libs/$archivesBaseName-${version}.jar")
    into file("E:\\Users\\minemobs\\IdeaProjects\\SpigotProject\\MinemobsUtils\\debug\\spigot\\plugins")
}

build.finalizedBy(copyFile)
shadowJar.finalizedBy(copyFile)
